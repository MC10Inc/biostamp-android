buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.10'
    }
}

plugins {
    id 'org.ajoberstar.grgit' version '4.0.1'
}

apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
apply plugin: 'maven-publish'

def libraryVersionName = "0.0.3-${grgit.head().getAbbreviatedId()}"

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 29
        versionCode grgit.log().size()
        versionName libraryVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven() {
        url 'https://jitpack.io'
    }
}

dependencies {
    def lifecycle_version = "2.2.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.google.protobuf:protobuf-javalite:3.9.1'
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'com.github.Fitbit:bitgatt:v0.9.1'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.9.1'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

android.libraryVariants.all { variant ->
    if (variant.name == 'debug') {
        task javadoc(type: Javadoc) {
            source = android.sourceSets.main.java.sourceFiles
            classpath = files(((Object) android.bootClasspath.join(File.pathSeparator)))
            classpath += variant.javaCompileProvider.get().classpath
        }
        task javadocJar(type: Jar, dependsOn: javadoc) {
            from javadoc.destinationDir
            classifier "javadoc"
        }
    }
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/mc10inc/biostamp-android")
                credentials {
                    username = project.findProperty("gpr.user")
                    password = project.findProperty("gpr.key")
                }
            }
        }
        publications {
            debug(MavenPublication) {
                from components.debug
                groupId = 'com.mc10inc.biostamp3'
                version = libraryVersionName
                artifactId = 'sdk'
                artifact sourceJar
                artifact javadocJar
            }
        }
    }
}
